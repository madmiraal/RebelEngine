#!/usr/bin/env python

Import("environment")

from platform_methods import run_in_subprocess
import main_builders

environment.main_sources = []

environment.add_source_files(environment.main_sources, "*.cpp")

# Order matters here. Higher index controller database files write on top of lower index database files.
controller_databases = ["gamecontrollerdb.txt", "rebelcontrollerdb.txt"]

gensource = environment.CommandNoCache(
    "default_controller_mappings.gen.cpp",
    controller_databases,
    run_in_subprocess(main_builders.make_default_controller_mappings),
)

environment.add_source_files(environment.main_sources, gensource)

environment.Depends("#main/splash.gen.h", "#main/splash.png")
environment.CommandNoCache(
    "#main/splash.gen.h",
    "#main/splash.png",
    run_in_subprocess(main_builders.make_splash),
)

if not environment["no_editor_splash"]:
    environment.Depends("#main/splash_editor.gen.h", "#main/splash_editor.png")
    environment.CommandNoCache(
        "#main/splash_editor.gen.h",
        "#main/splash_editor.png",
        run_in_subprocess(main_builders.make_splash_editor),
    )

environment.Depends("#main/app_icon.gen.h", "#main/app_icon.png")
environment.CommandNoCache(
    "#main/app_icon.gen.h",
    "#main/app_icon.png",
    run_in_subprocess(main_builders.make_app_icon),
)

if environment["tools"]:
    SConscript("tests/SCsub")

lib = environment.add_library("main", environment.main_sources)
environment.Prepend(LIBS=[lib])
